plugins {
    id 'java'
}

apply from: 'publishing.gradle'
apply from: 'lint.gradle'

subprojects {
    group = "${groupId}"
    version = "${projectVersion}"

    repositories {
        mavenCentral()
    }

    sourceCompatibility = project.javaVersion
    targetCompatibility = project.javaVersion

    apply plugin: 'java'
    apply plugin: 'jacoco'

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
    }

    test {
        useJUnitPlatform()
    }

    check {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
        finalizedBy jacocoTestCoverageVerification
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }
                limit {
                    counter = 'INSTRUCTION'
                    value = 'COVEREDRATIO'
                    minimum = 1
                }
            }
        }
    }
}