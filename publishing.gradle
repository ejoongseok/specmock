sourceCompatibility = project.javaVersion
targetCompatibility = project.javaVersion

jar.enabled = false

if (project.hasProperty('allowPublishing')) {
    subprojects {
        apply plugin: 'java'
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        tasks.register('sourcesJar', Jar) {
            from sourceSets.main.allSource
            archiveClassifier.set("sources")
        }
        tasks.register('javadocJar', Jar) {
            from javadoc.destinationDir
            archiveClassifier.set("javadoc")
        }
        signing {
            sign configurations.archives
        }
        tasks.register('publishBuild', GradleBuild) {
            group = 'publishing'
            dependsOn 'clean', 'build'
        }
        publish {
            dependsOn 'publishBuild'
        }
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifactId = project.name
                    group = project.groupId
                    version = project.projectVersion
                    from components.java

                    artifact sourcesJar
                    artifact javadocJar

                    pom {
                        name = project.groupId
                        description = 'SpecMock provides Mock Server for various specs, offering a lightweight, fast, and easy-to-use experience.'
                        url = 'https://github.com/specmock/specmock'

                        scm {
                            connection = 'scm:git:git://github.com/specmock/specmock.git'
                            developerConnection = 'scm:git:https://github.com/specmock/specmock.git'
                            url = 'https://github.com/specmock/specmock'
                        }
                        licenses {
                            license {
                                name = 'The Apache License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        developers {
                            developer {
                                id = 'SpecMock'
                                name = 'SpecMock'
                                email = 'official.specmock@gmail.com'
                            }
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = 'sonatype'
                    url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

                    def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases/'
                    def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = project.projectVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                    credentials {
                        username ossrhUsername
                        password ossrhPassword
                    }
                }
            }
        }
    }
}